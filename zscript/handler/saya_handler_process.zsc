// Red-Eyed Rampage event processing

extend Class SWWMHandler
{
	transient int WeaponFlash[10];
	transient int spellchangetic;
	transient ui int lastspell;

	private ui void ModConsoleProcess( ConsoleEvent e )
	{
		// nothing here yet
	}

	private void ModNetworkProcess( ConsoleEvent e )
	{
		if ( e.Name ~== "sayaweaponreceive" )
		{
			if ( (e.Args[0] < 0) || (e.Args[0] > 9) ) return;
			WeaponFlash[e.Args[0]] = gametic+25;
		}
		else if ( e.Name ~== "sayaspellslot" )
		{
			if ( (e.Args[0] < -1) || (e.Args[0] > 3) ) return;
			let saya = SayaPlayer(players[e.player].mo);
			if ( saya )
			{
				if ( e.Args[0] == -1 ) saya.SpellSel = (saya.SpellSel+1)%4;
				else saya.SpellSel = e.Args[0];
				if ( e.player == consoleplayer )
					spellchangetic = gametic+1;
			}
		}
	}

	override void UiTick()
	{
		if ( gametic != spellchangetic ) return;
		let saya = SayaPlayer(players[consoleplayer].mo);
		if ( !saya ) return;
		if ( saya.SpellSel != lastspell )
		{
			let bar = SWWMStatusBar(statusbar);
			if ( bar )
			{
				bar.ntagstr = StringTable.Localize("$T_SAYASPELLCARD"..(saya.SpellSel+1));
				bar.ntagtic = level.totaltime;
				bar.ntagcol = nametagcolor;
			}
		}
		lastspell = saya.SpellSel;
	}
}
