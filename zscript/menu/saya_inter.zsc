// intermission stat screen
Class SayaStatScreen : SWWMStatScreen
{
	TextureID bgtex;
	TextureID sayatex[3];
	TitleSmoke smk[128];
	TextureID smk_tex[8];

	override void Start( wbstartstruct wbstartstruct )
	{
		Super.Start(wbstartstruct);
		lnamecolor = Font.CR_WHITE;
		lauthcolor = Font.CR_DARKGRAY;
		lsubcolor = Font.CR_RED;
		statbasecolor = Font.CR_RED;
		statcolor0 = Font.CR_WHITE;
		statcolor1 = Font.CR_GOLD;
		statcolor2 = Font.CR_DARKRED;
		tipcolor0 = Font.CR_BRICK;
		tipcolor1 = Font.CR_WHITE;
	}

	override void StartMusic()
	{
		S_ChangeMusic("music/saya_intermus.ogg");
	}

	override String StatCnt( int a, int b )
	{
		if ( b <= 0 ) return "N/A";
		return String.Format("%s \cu/\c- %s \cu(\c-%3d%%\cu)",SWWMUtility.ThousandsNum(max(a,0)),SWWMUtility.ThousandsNum(b),GetPct(a,b));
	}

	override String TimeStr( int secs )
	{
		secs = max(secs,0);
		int h = secs/3600;
		int m = (secs/60)%60;
		int s = secs%60;
		if ( h ) return String.Format("%d\cu:\c-%02d\cu:\c-%02d",h,m,s);
		if ( m ) return String.Format("%d\cu:\c-%02d",m,s);
		return String.Format("%d",s);
	}

	override void Ticker( void )
	{
		Super.Ticker();
		TickSmokeLayers();
	}

	private void TickSmokeLayers()
	{
		for ( int i=0; i<128; i++ )
		{
			int presim = 0;
			if ( smk[i].age >= smk[i].maxage )
			{
				if ( smk[i].maxage == 0 ) presim = Random[UIStuff](0,60);
				else presim = -1;
				smk[i].age = Random[UIStuff](-20,0);
				smk[i].maxage = Random[UIStuff](60,120);
				smk[i].pos = (FRandom[UIStuff](-512.,512.),FRandom[UIStuff](0.,256.));
				smk[i].vel = (FRandom[UIStuff](-1.,3.),FRandom[UIStuff](-2.,-6.));
				smk[i].ang = FRandom[UIStuff](0.,360.);
				smk[i].angvel = FRandom[UIStuff](1.,4.)*RandomPick[UIStuff](-1,1);
				smk[i].scale = FRandom[UIStuff](2.,4.);
			}
			for ( int j=0; j<=presim; j++ )
			{
				smk[i].age++;
				if ( smk[i].age <= 0 ) continue;
				smk[i].pos += smk[i].vel;
				smk[i].ang += smk[i].angvel;
			}
		}
	}
	private void RenderSmokeLayer( double GameSecs, bool back, double alpha = 1., double disp = 0., bool zoomout = false )
	{
		double zoomscl = Screen.GetHeight()/(zoomout?600:500.);
		int imin = back?64:0;
		int imax = back?128:64;
		int base = zoomout?(back?450:750):(back?500:300);
		for ( int i=imin; i<imax; i++ )
		{
			if ( !smk_tex[i%8] ) smk_tex[i%8] = TexMan.CheckForTexture("graphics/title_smoke_"..(i%8)..".png",TexMan.Type_Any);
			Vector2 pos = (smk[i].pos+smk[i].vel*fractic+(0,base+disp))*zoomscl+(Screen.GetWidth()/2,0);
			double scl = smk[i].scale*zoomscl;
			double ang = smk[i].ang+smk[i].angvel*fractic;
			double alf = cos((smk[i].age/double(smk[i].maxage))*90)*alpha;
			alf *= clamp(smk[i].age/10.,0,1);
			Screen.DrawTexture(smk_tex[i%8],false,pos.x,pos.y,DTA_CenterOffset,true,DTA_Rotate,ang,DTA_ScaleX,scl,DTA_ScaleY,scl,DTA_Alpha,alf,DTA_LegacyRenderStyle,STYLE_AddShaded,DTA_FillColor,Color(64,16,4));
		}
	}
	override void drawSWWMBg()
	{
		double GameSecs = (bcnt+fractic)/double(GameTicRate);
		if ( swwm_fuzz )
		{
			if ( !bgtex ) bgtex = TexMan.CheckForTexture("graphics/tempbg.png",TexMan.Type_Any);
			Vector2 tsize = TexMan.GetScaledSize(bgtex);
			double zoom = max(ceil(Screen.GetWidth()/tsize.x),ceil(Screen.GetHeight()/tsize.y));
			Vector2 vsize = (Screen.GetWidth(),Screen.GetHeight())/zoom;
			Screen.DrawTexture(bgtex,false,(vsize.x-tsize.x)/2,(vsize.y-tsize.y)/2,DTA_VirtualWidthF,vsize.x,DTA_VirtualHeightF,vsize.y,DTA_KeepRatio,true,DTA_ColorOverlay,Color(128,0,0,0));
			Screen.Dim(Color(16,0,0),1.-clamp(GameSecs,.1,1.),0,0,Screen.GetWidth(),Screen.GetHeight());
		}
		else Screen.Dim(Color(16,0,0),1.,0,0,Screen.GetWidth(),Screen.GetHeight());
		double texscl = Screen.GetHeight()/1500.;
		if ( !gradtex ) gradtex = TexMan.CheckForTexture("graphics/title_grad.png",TexMan.Type_Any);
		Screen.DrawTexture(gradtex,false,0,Screen.GetHeight(),DTA_DestWidth,Screen.GetWidth(),DTA_DestHeight,512*CleanYFac_1,DTA_LegacyRenderStyle,STYLE_AddShaded,DTA_FillColor,Color(128,0,0),DTA_TopOffset,256);
		RenderSmokeLayer(GameSecs,true,.5,0,true);
		if ( !sayatex[0] ) sayatex[0] = TexMan.CheckForTexture("graphics/title_saya.png",TexMan.Type_Any);
		if ( !sayatex[1] ) sayatex[1] = TexMan.CheckForTexture("graphics/title_saya_smoke.png",TexMan.Type_Any);
		if ( !sayatex[2] ) sayatex[2] = TexMan.CheckForTexture("graphics/title_saya_eye.png",TexMan.Type_Any);
		double ofs;
		if ( sp_state >= 2 ) ofs = 120;
		else ofs = 120*SWWMUtility.smerp(clamp(GameSecs,0.,1.));
		Screen.DrawTexture(sayatex[0],false,Screen.GetWidth()/2+ofs*CleanXFac_1,Screen.GetHeight()+700*texscl,DTA_ScaleX,texscl,DTA_ScaleY,texscl);
		Screen.DrawTexture(sayatex[1],false,Screen.GetWidth()/2+ofs*CleanXFac_1,Screen.GetHeight()+700*texscl,DTA_ScaleX,texscl,DTA_ScaleY,texscl,DTA_LegacyRenderStyle,STYLE_Add);
		Screen.DrawTexture(sayatex[2],false,Screen.GetWidth()/2+ofs*CleanXFac_1,Screen.GetHeight()+700*texscl,DTA_ScaleX,texscl,DTA_ScaleY,texscl,DTA_LegacyRenderStyle,STYLE_Add);
		double alf = clamp((glarestr-fractic)/20.,0.,1.)**2;
		if ( alf > 0. ) Screen.DrawTexture(sayatex[2],false,Screen.GetWidth()/2+ofs*CleanXFac_1,Screen.GetHeight()+700*texscl,DTA_ScaleX,texscl,DTA_ScaleY,texscl,DTA_LegacyRenderStyle,STYLE_Add,DTA_Alpha,alf);
		RenderSmokeLayer(GameSecs,false,1.,0,true);
		Screen.DrawTexture(gradtex,false,0,Screen.GetHeight(),DTA_DestWidth,Screen.GetWidth(),DTA_DestHeight,64*CleanYFac_1,DTA_LegacyRenderStyle,STYLE_Shaded,DTA_FillColor,Color(128,0,0),DTA_TopOffset,256);
	}
}
